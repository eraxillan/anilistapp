plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

group = 'com.github.eraxillan'
version = '1.0'

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        // Sets Java compatibility to Java 11
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()

        // 1) Allow experimental API usage directly in code block, without entire function marking
        // 2) Enable explicit API mode (check https://kotlinlang.org/docs/whatsnew14.html#explicit-api-mode-for-library-authors)
        freeCompilerArgs += [
            "-Xopt-in=kotlin.RequiresOptIn",
            "-Xexplicit-api=strict"
        ]
    }
}

dependencies {
    // Java 8+ API "desugaring" support: allow using new APIs on older versions of Android
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    // Advanced logging
    implementation "com.jakewharton.timber:timber:4.7.1"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release"
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.eraxillan'
                artifactId = 'android-chippededittext-view'
                version = '1.0'
            }
        }
    }
}
